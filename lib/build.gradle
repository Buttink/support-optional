plugins {
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'com.jfrog.bintray' version '1.7.3'
}

group 'com.github.dmstocking'
version '1.2'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

/*
 * If you wish to publish builds, you must provide a gradle.properties
 * file in "app" that has two lines
 *
 * BINTRAY_USER=<real user here>
 * BINTRAY_KEY=<real key here>
 */
def bintrayUser = hasProperty('BINTRAY_USER') ? BINTRAY_USER : "Do not fill in"
def bintrayKey = hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : "Do not fill in"

compileJava {
    sourceCompatibility = 1.5
    targetCompatibility = 1.5
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId 'com.github.dmstocking'
            artifactId 'support-optional'
            version '1.2'
            artifact (sourcesJar) {
                classifier = 'sources'
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

bintray {
    user bintrayUser
    key bintrayKey
    publications = ['mavenJava']
    pkg {
        repo = 'support-optional'
        name = 'support-optional'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/dmstocking/support-optional.git'
        version {
            name = '1.2'
            desc = 'support-optional 1.2'
            vcsTag = '1.2'
        }
    }
}
